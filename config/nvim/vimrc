set notermguicolors

set nocompatible
filetype plugin indent on
syntax on

let mapleader = ";"
let maplocalleader = mapleader

set modelines=5
set bs=indent,eol,start

" one space after period
set nojoinspaces
" Ignore files when completing
set suffixes+=.class,.pyc,.pyo
set suffixes+=.lo,.swo
" Don't ignore headers when completing
set suffixes-=.h

" show line number and character pos
set ruler
set sts=4
" keep 4 lines above and below the cursor
set so=4

" I always lose the cursor, sigh
set cursorline

" show input commands as they're being typed
set showcmd

set signcolumn=yes

" complete recursively in subdirs
set path+=**
" unexplainable but helpful completion settings
set wildmenu wildmode=list,list:longest
set wildignore+=/.git/**

" always include status line
set laststatus=2

" don't remember options in seccions
set sessionoptions-=options

" don't let search open a fold
" set fdo-=search

" help ins-completion
" C-n, C-p are what to default to

" i have cores for a reason
set makeprg=make\ -j8

" my simple statusline, airline was a steaming pile
set statusline=%q%t                              " filename
set statusline+=\ @\ %P                          " percentage of the way thru file
set statusline+=\ [ft=%Y%M%R%W%H]                " filetype
set statusline+=\ char:0x%B                      " hex of character under cursor
set statusline+=\ pos\ %l:%c                     " line:column position of cursor
set statusline+=\ %{wordcount()['words']}\ words " word count
set statusline+=\ %=%<%{expand('%:~:.:h')}       " directory

" larger command display
set cmdheight=2

"set tags=./tags,./TAGS,tags,TAGS,./.tags,./.TAGS,.tags,.TAGS,../../tags,../tags
let &tags.=expand(",$TAGS")
" R mode customizations
let R_assign = 2

set grepprg=rg\ --vimgrep\ $*
set grepformat=%f:%l:%c:%m

" functions
function s:camel_word(dir)
    call search('\U\zs\u\|\<', a:dir)
endfunction


" Global mappings {{{
" disable keymapping for Ex mode
nnoremap Q <nop>

" one of my habits is to I<type><CR><type> or whatever and this can introduce
" whitespace errors. let's try this.
" nnoremap I<CR> O<Esc>jI

" functions from junegunn-fzf.vim
" each brings up fuzzy completion list
" <Leader>u - list of buffers
nnoremap <Leader>u :Buffers<CR>
" <Leader>f - list of files under the current Git repo
nnoremap <Leader>F :<C-u>GitFiles<CR>
" <Leader>f - list of files under the current directory
nnoremap <Leader>f :<C-u>Files<CR>
" <Leader>f - list of tags
nnoremap <Leader>t :Tags<CR>

" <Leader>d deletes the current buffer
" nnoremap <Leader>d :bd<CR>

" Insert current date into buffer. Used for note taking.
nnoremap <Leader>it "=strftime("%c")<CR>p

" select some text, then type // and it will search for the literal text
vnoremap // y/\V<C-R>"<CR>

" highlight matches in search
" set hlsearch
" <Leader>h will turn off highlights
nnoremap <Leader>h :set hls!<CR>
"nnoremap <Space> zz

" close the current window
nnoremap <Leader>c <C-w>c
" make the current window the only window
nnoremap <Leader>o <C-w>o
" easier movement in splits taking into account dvorak
" nnoremap <C-h> <C-w><C-h>
" nnoremap <C-t> <C-w>k
" nnoremap <C-n> <C-w>j
" nnoremap <C-s> <C-w><C-l>

" C-w ] will open tag in a split
" C-w g } will let you select tag for preview
nnoremap <C-n> :lua vim.diagnostic.goto_next()<CR>
nnoremap <C-p> :lua vim.diagnostic.goto_prev()<CR>
nnoremap <Leader>n :cnext<CR>
nnoremap <Leader>p :cprev<CR>

" Put timestamp in filename?
cmap <F3> <C-R>=strftime("%Y%m%d%H%M")<CR>

nnoremap <silent> _ :aboveleft sp<CR>:exe "normal \<Plug>VinegarUp"<CR>
nnoremap <silent> <Bar> :aboveleft vsp<CR>:exe "normal \<Plug>VinegarUp"<CR>
" }}}

command CtagsCpp !ctags --c++-kinds=+p --c-kinds=+p --fields=+iaS --extra=+q -Rnu .

command HighlightCurrentLine call matchadd('Search', '\%'.line('.').'l')
command ClearHighlightCurrentLine call clearmatches()

" https://vim.fandom.com/wiki/Search_across_multiple_lines
" Search for the ... arguments separated with whitespace (if no '!'),
" or with non-word characters (if '!' added to command).
function! SearchMultiLine(bang, ...)
  if a:0 > 0
    let sep = (a:bang) ? '\_W\+' : '\_s\+'
    let @/ = join(a:000, sep)
  endif
endfunction
command! -bang -nargs=* -complete=tag S call SearchMultiLine(<bang>0, <f-args>)|normal! /<C-R>/<CR>

" The source for these happens in shell.nix
command! ZettelFiles :call ZettelFiles()
command! ZettelNew :call ZettelNew(1)
command! ZettelNewLinkBack :call ZettelNewLinkBack()
command! ZettelFindBackLinks :call ZettelFindLinksTo()
command! ZettelFollowLink :call ZettelFzfRgLinkSource()

" <Leader>j is the journal prefix
let g:journal_dir = 'journal/'

command! -complete=dir -nargs=1 ReadJournalFilesInDir call ReadJournalFilesInDirectory(<f-args>)
command! -complete=customlist,s:completeDate -nargs=1 CreateJournalFileByDate call CreateJournalFileByDate(<q-args>)
function! s:completeDate(ArgLead, CmdLine, CursorPos)
    if a:ArgLead =~# '^20\d\d$'
        return [a:ArgLead . '/']
    elseif a:ArgLead =~# '^20\d\d/$'
        let current_year = str2nr(split(a:ArgLead, '/')[0])
        let month_list = []
        for month in range(1, 12)
          let month_str = printf('%02d', month)
          let month_list += [month_str . '/']
        endfor
        return month_list
        "return [a:ArgLead . strftime('%m')]
    elseif a:ArgLead =~# '^20\d\d/\d\d/$'
        let arg_split = split(a:ArgLead, '/')
        let current_year = str2nr(arg_split[0])
        let current_month = str2nr(arg_split[1])
        let month_days = s:monthDays(current_year, current_month)
        let day_list = []
        for day in range(1, month_days)
            let day_str = printf('%02d', day)
            let day_list += [day_str]
        endfor
        return day_list
    elseif a:ArgLead =~# '^$'
        return [strftime('%Y')]
    else
        return []
    endif
endfunction

function! s:monthDays(year, month)
  let leap = (a:year % 4 == 0 && a:year % 100 != 0) || a:year % 400 == 0
  if a:month == 2
    return leap ? 29 : 28
  elseif index([4, 6, 9, 11], a:month) != -1
    return 30
  else
    return 31
  endif
endfunction

nnoremap <Leader>je :call CreateJournalFile()<CR>
nnoremap <Leader>jn :CreateJournalFileByDate 
nnoremap <Leader>jr :call ShowRecentJournalFiles()<CR>
nnoremap <Leader>js :call ReadJournalFilesInDirectory(g:journal_dir . strftime("%Y") . '/' . strftime("%m"))<CR>

" package fzf-vim {{{
" :BuffersDelete function to use fzf to delete buffers
function! s:list_buffers()
    redir => list
    silent ls
    redir END
    return split(list, "\n")
endfunction

function! s:delete_buffers(lines)
    execute 'bwipeout' join(map(a:lines, {_, line -> split(line)[0]}))
endfunction

" Call :BuffersDelete to pop up window of buffers to delete, use tab to
" select a buffer for deletion, enter to delete all
command! BuffersDelete call fzf#run(fzf#wrap({
            \ 'source': s:list_buffers(),
            \ 'sink*': { lines -> s:delete_buffers(lines) },
            \ 'options': '--multi --reverse --bind ctrl-a:select-all+accept'
            \ }))

" https://github.com/junegunn/fzf.vim/issues/556
" remap `gf` to pick up files anywhere inside current directory rather than
" just the literal `<cfile>` when you want the same for some *other*
" directory, you put your cursor on the filename and type `:GF other-dir`
function! GF(...)
    call fzf#run({'dir': a:1, 'source': 'find . -type f', 'options':['-1', '--query', expand('<cfile>')], 'sink': 'e'})
endfunction
command! -nargs=* GF :call GF(<f-args>)

" goto fzf file at cursor
" nnoremap gf :call fzf#vim#files('.', {'options':'-1 --query '.expand('<cword>')})<CR>

" search from cwd
nnoremap \ :Rg<CR>
" fzf over lines in current buffer, :Lines for all buffers
nnoremap <Leader>b :BLines<CR>
nnoremap <Leader>B :Lines<CR>
" }}}

" package vimtex {{{
let g:tex_flavor = "latex"
let g:vimtex_disable_recursive_main_file_detection = 1
" }}}

" GUI options {{{
if has('gui')
    set guioptions-=m " no menu
    set guioptions-=T " no toolbar
    set guioptions-=r " no scrollbars
    set guioptions-=R
    set guioptions-=l
    set guioptions-=L
    set guioptions-=b
    set guifont=Iosevka\ Term\ Medium:h13
    " set guifont=SF\ Mono\ Regular:h13
    " set guifont=Source\ Code\ Pro\ Regular:h13
endif
" }}}

" load all the vim packages now, including pathogen
packloadall

" Python indenting {{{
" function PythonParenIndent(lnum)
"     call cursor(a:lnum, 1)
"     let [pline, pcol] = searchpairpos('(\|{\|\[', '', ')\|}\|\]', 'bW',
"                         \ "synIDattr(synID(line('.'), col('.'), 1), 'name')" .
"                         \ " =~ '\\(Comment\\|String\\)$'",
"                         \ max([0,a:lnum-50]))
"     let plinecontent = getline(pline)
"     let lineend = match(plinecontent, '\s*$')
"     if pcol >= lineend
"         return &sw
"     else
"         return pcol-indent(pline)
"     endif
" endfunction
" 
" let g:pyindent_nested_paren='PythonParenIndent(a:lnum)'
" let g:pyindent_open_paren='PythonParenIndent(a:lnum)'
" }}}

" z3 specific style settings for c++
autocmd BufRead
     \ ~/work/inprogress/z3/*/{src,tools,tests}/*.{cc,cpp,h,inc}
     \ setlocal makeprg=make\ -C\ .vimbuild\ -j24\ all sw=4 cino=:0,l1,g0,t0,(0,w1,W4

" Trailing whitespace
command StripTrailingWhitespace %s/\s\+$//e

" Goyo config {{{
let g:goyo_width = 120

function! s:goyo_enter()
    set lbr
endfunction

function! s:goyo_leave()
    set nolbr
endfunction

autocmd! User GoyoEnter nested call <SID>goyo_enter()
autocmd! User GoyoLeave nested call <SID>goyo_leave()
" }}}


runtime vimrc_local

